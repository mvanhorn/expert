defmodule Engine.Build.State do
  import Forge.EngineApi.Messages

  alias Engine.Build
  alias Engine.Compilation.ProjectTracer
  alias Engine.Compilation.TraceBuffer
  alias Engine.Compilation.Tracers
  alias Engine.Plugin
  alias Forge.Document
  alias Forge.Project
  alias Forge.VM.Versions

  require Logger

  defstruct project: nil,
            build_number: 0,
            uri_to_document: %{},
            project_compile: :none,
            last_deps_fetch_result: nil,
            runtime_refresh_project: nil

  def new(%Project{} = project) do
    %__MODULE__{project: project}
  end

  def on_timeout(%__MODULE__{} = state) do
    new_state =
      case state.project_compile do
        :none -> state
        :force -> compile_project(state, true)
        :normal -> compile_project(state, false)
      end

    # We need to compile the individual documents even after the project is
    # compiled because they might have unsaved changes, and we want that state
    # to be the latest state of the project.
    new_state =
      Enum.reduce(new_state.uri_to_document, new_state, fn {_uri, document}, state ->
        compile_file(state, document)
      end)

    %{new_state | uri_to_document: %{}, project_compile: :none}
  end

  def on_file_compile(%__MODULE__{} = state, %Document{} = document) do
    %__MODULE__{
      state
      | uri_to_document: Map.put(state.uri_to_document, document.uri, document)
    }
  end

  def on_project_compile(%__MODULE__{} = state, force?) do
    project_compile = if force?, do: :force, else: :normal

    %__MODULE__{state | project_compile: project_compile}
  end

  def ensure_build_directory(%__MODULE__{} = state) do
    # If the project directory isn't there, for some reason the main build fails, so we create it here
    # to ensure that the build will succeed.
    project = state.project
    build_path = Project.versioned_build_path(project)

    case Versions.check_erlang_compatibility(build_path) do
      :compatible ->
        :ok

      {:incompatible, tagged, current} ->
        Logger.info(
          "Build path #{build_path} was compiled with Erlang #{tagged}, " <>
            "but current Erlang is #{current}. Deleting"
        )

        File.rm_rf(build_path)

      :untagged ->
        :ok

      {:unreadable, _error} ->
        Logger.info("Build path #{build_path} has unreadable version tags. Deleting")
        File.rm_rf(build_path)
    end

    maybe_delete_old_builds(project)

    if !File.exists?(build_path) do
      File.mkdir_p!(build_path)
      Versions.write(build_path)
    end
  end

  def fetch_deps(%__MODULE__{} = state, project) do
    build_path = Project.versioned_build_path(project)

    Logger.info("Cleaning build directory: #{build_path}")

    case File.rm_rf(build_path) do
      {:ok, _} ->
        :ok

      {:error, reason, path} ->
        Logger.warning("Failed to remove build path #{path}: #{inspect(reason)}")
    end

    result =
      project
      |> Engine.Build.Project.fetch_deps()
      |> normalize_fetch_deps_result()

    %{state | last_deps_fetch_result: result}
  end

  def last_deps_fetch_result(%__MODULE__{last_deps_fetch_result: result}), do: result

  defp normalize_fetch_deps_result({:ok, :ok}), do: :ok
  defp normalize_fetch_deps_result(result), do: result

  defp compile_project(%__MODULE__{} = state, initial?) do
    state = increment_build_number(state)
    project = state.project

    Build.with_lock(fn ->
      compile_requested_message =
        project_compile_requested(project: project, build_number: state.build_number)

      Engine.broadcast(compile_requested_message)
      {elapsed_us, result} = :timer.tc(fn -> Build.Project.compile(project, initial?) end)
      elapsed_ms = to_ms(elapsed_us)

      {status, diagnostics} = project_compile_result(result)
      trace_result = settle_project_trace(project, status)
      log_trace_result(trace_result, "project compile")

      compile_message =
        project_compiled(status: status, project: project, elapsed_ms: elapsed_ms)

      diagnostics_message =
        project_diagnostics(
          project: project,
          build_number: state.build_number,
          diagnostics: diagnostics
        )

      Engine.broadcast(compile_message)
      Engine.broadcast(diagnostics_message)
      Plugin.diagnose(project, state.build_number)
    end)

    mark_runtime_refresh_pending(state)
  end

  def on_project_index_ready(%__MODULE__{} = state, %Project{} = ready_project) do
    refresh_runtime(state, ready_project)
  end

  def on_project_index_ready(%__MODULE__{} = state, _project), do: state

  defp project_compile_result(:ok), do: {:success, []}
  defp project_compile_result({:ok, diagnostics}), do: {:success, List.wrap(diagnostics)}
  defp project_compile_result({:error, diagnostics}), do: {:error, List.wrap(diagnostics)}

  def compile_file(%__MODULE__{} = state, %Document{} = document) do
    state = increment_build_number(state)
    project = state.project

    Build.with_lock(fn ->
      Engine.broadcast(file_compile_requested(uri: document.uri))

      {elapsed_us, result} = :timer.tc(fn -> compile_document(project, document) end)

      elapsed_ms = to_ms(elapsed_us)

      {status, diagnostics} = file_compile_result(result)
      trace_result = settle_file_trace(project, document, status)
      log_trace_result(trace_result, "file compile")

      compile_message =
        file_compiled(
          project: project,
          build_number: state.build_number,
          status: status,
          uri: document.uri,
          elapsed_ms: elapsed_ms
        )

      diagnostics =
        file_diagnostics(
          project: project,
          build_number: state.build_number,
          uri: document.uri,
          diagnostics: List.wrap(diagnostics)
        )

      Engine.broadcast(compile_message)
      Engine.broadcast(diagnostics)
      Plugin.diagnose(project, state.build_number, document)
    end)

    state
  end

  defp file_compile_result({:ok, diagnostics}), do: {:success, diagnostics}
  defp file_compile_result({:error, diagnostics}), do: {:error, diagnostics}

  defp settle_project_trace(%Project{} = project, :success),
    do: TraceBuffer.commit_project(project)

  defp settle_project_trace(%Project{} = project, _status),
    do: TraceBuffer.discard_project(project)

  defp settle_file_trace(%Project{} = project, %Document{} = document, :success) do
    TraceBuffer.commit_path(project, document.path, dirty_source?: true)
  end

  defp settle_file_trace(_project, %Document{} = document, _status) do
    TraceBuffer.discard(document.path)
  end

  defp log_trace_result(:ok, _operation), do: :ok

  defp log_trace_result({:error, reason}, operation) do
    Logger.warning("Failed to commit trace data for #{operation}: #{inspect(reason)}")
  end

  defp compile_document(%Project{kind: :mix} = project, document) do
    Engine.Mix.in_project(project, fn _ ->
      Tracers.with_project(project, [ProjectTracer], [buffer_beam_paths?: false], fn ->
        Build.Document.compile(document)
      end)
    end)
  end

  defp compile_document(%Project{}, document) do
    Build.Document.compile(document)
  end

  def set_compiler_options do
    Code.compiler_options(
      debug_info: true,
      parser_options: [columns: true, token_metadata: true]
    )

    :ok
  end

  def mix_compile_opts(initial?) do
    opts = ~w(
        --return-errors
        --ignore-module-conflict
        --all-warnings
        --docs
        --debug-info
        --no-protocol-consolidation
    )

    if initial? do
      ["--force " | opts]
    else
      opts
    end
  end

  defp to_ms(microseconds) do
    microseconds / 1000
  end

  defp increment_build_number(%__MODULE__{} = state) do
    %__MODULE__{state | build_number: state.build_number + 1}
  end

  defp refresh_runtime(
         %__MODULE__{runtime_refresh_project: %Project{} = pending_project} = state,
         %Project{} = ready_project
       )
       when pending_project == ready_project do
    Engine.Build.Project.refresh_runtime(ready_project)
    %__MODULE__{state | runtime_refresh_project: nil}
  end

  defp refresh_runtime(%__MODULE__{} = state, _project), do: state

  defp mark_runtime_refresh_pending(%__MODULE__{project: %Project{kind: :mix} = project} = state) do
    %__MODULE__{state | runtime_refresh_project: project}
  end

  defp mark_runtime_refresh_pending(%__MODULE__{} = state), do: state

  @two_month_seconds 86_400 * 31 * 2
  defp maybe_delete_old_builds(%Project{} = project) do
    build_root = Project.build_path(project)
    two_months_ago = System.system_time(:second) - @two_month_seconds

    case File.ls(build_root) do
      {:ok, entries} ->
        for file_name <- entries,
            absolute_path = Path.join(build_root, file_name),
            File.dir?(absolute_path),
            newest_beam_mtime(absolute_path) <=
              two_months_ago do
          File.rm_rf!(absolute_path)
        end

      _ ->
        :ok
    end
  end

  defp newest_beam_mtime(directory) do
    beam_files = directory |> Path.join("**/*.beam") |> Path.wildcard()

    case beam_files do
      [] ->
        0

      beam_files ->
        beam_files
        |> Enum.map(&File.stat!(&1, time: :posix).mtime)
        |> Enum.max()
    end
  end
end
