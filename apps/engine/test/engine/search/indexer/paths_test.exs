defmodule Engine.Search.Indexer.PathsTest do
  use ExUnit.Case, async: false

  alias Engine.Search.Indexer.Paths
  alias Forge.Project

  describe "indexable_files/1" do
    @tag :tmp_dir
    test "does not include project-local default build files", %{tmp_dir: tmp_dir} do
      with_env("MIX_BUILD_PATH", Path.join([tmp_dir, ".expert", "build", "dev"]))

      source_file = Path.join([tmp_dir, "lib", "source_file.ex"])
      build_file = mix_build_file!(tmp_dir, "generated.ex")

      write_mix_project!(
        tmp_dir,
        "DefaultBuildPathIndexerTest.MixProject",
        ~s([app: :default_build_path_indexer_test, version: "0.1.0"])
      )

      write_file!(source_file, "defmodule SourceFile do end")
      write_file!(build_file, "defmodule GeneratedBuildFile do end")

      project = project(tmp_dir)

      assert source_file in Paths.indexable_files(project)
      refute build_file in Paths.indexable_files(project)
    end

    @tag :tmp_dir
    test "does not include files under a configured build path", %{tmp_dir: tmp_dir} do
      with_env("MIX_BUILD_PATH", Path.join([tmp_dir, ".expert", "build", "dev"]))

      source_file = Path.join([tmp_dir, "lib", "source_file.ex"])
      build_file = mix_build_file!(tmp_dir, "generated.ex", build_path: "custom_build")

      write_mix_project!(
        tmp_dir,
        "ConfiguredBuildPathIndexerTest.MixProject",
        ~s([app: :configured_build_path_indexer_test, version: "0.1.0", build_path: "custom_build"])
      )

      write_file!(source_file, "defmodule SourceFile do end")
      write_file!(build_file, "defmodule GeneratedBuildFile do end")

      project = project(tmp_dir)

      assert source_file in Paths.indexable_files(project)
      refute build_file in Paths.indexable_files(project)
    end

    @tag :tmp_dir
    test "does not include files under MIX_BUILD_ROOT", %{tmp_dir: tmp_dir} do
      build_root = Path.join(tmp_dir, "custom_build_root")
      with_env("MIX_BUILD_ROOT", build_root)
      with_env("MIX_BUILD_PATH", Path.join([tmp_dir, ".expert", "build", "dev"]))

      source_file = Path.join([tmp_dir, "lib", "source_file.ex"])
      build_file = Path.join(build_root, "generated.ex")

      write_mix_project!(
        tmp_dir,
        "MixBuildRootIndexerTest.MixProject",
        ~s([app: :mix_build_root_indexer_test, version: "0.1.0"])
      )

      write_file!(source_file, "defmodule SourceFile do end")
      write_file!(build_file, "defmodule GeneratedBuildFile do end")

      project = project(tmp_dir)

      assert source_file in Paths.indexable_files(project)
      refute build_file in Paths.indexable_files(project)
    end

    @tag :tmp_dir
    test "does not include active path dependency source files", %{tmp_dir: tmp_dir} do
      app_root = Path.join(tmp_dir, "app")
      dep_root = Path.join(tmp_dir, "dep")
      app_file = Path.join([app_root, "lib", "app_module.ex"])
      dep_file = Path.join([dep_root, "lib", "dep_module.ex"])

      write_mix_project!(
        app_root,
        "PathDependencyPathsTest.MixProject",
        ~s([app: :path_dependency_paths_test, version: "0.1.0", deps: [{:dep, path: "../dep"}]])
      )

      write_mix_project!(
        dep_root,
        "PathDependencyPathsTest.DepMixProject",
        ~s([app: :dep, version: "0.1.0"])
      )

      write_file!(app_file, "defmodule AppModule do end")
      write_file!(dep_file, "defmodule DepModule do end")

      project = project(app_root)

      assert app_file in Paths.indexable_files(project)
      refute dep_file in Paths.indexable_files(project)
    end
  end

  describe "project_source?/2" do
    @tag :tmp_dir
    test "matches project .ex files and excludes scripts, build outputs, deps, and workspace files",
         %{
           tmp_dir: tmp_dir
         } do
      project = project(tmp_dir)

      assert Paths.project_source?(project, Path.join([tmp_dir, "lib", "source.ex"]))

      refute Paths.project_source?(project, Path.join([tmp_dir, "lib", "script.exs"]))
      refute Paths.project_source?(project, Path.join([tmp_dir, "_build", "generated.ex"]))
      refute Paths.project_source?(project, Path.join([tmp_dir, "deps", "dep", "lib", "dep.ex"]))
      refute Paths.project_source?(project, Path.join([tmp_dir, ".expert", "generated.ex"]))
      refute Paths.project_source?(project, Path.join([tmp_dir <> "_other", "source.ex"]))
    end

    @tag :tmp_dir
    test "excludes configured build paths", %{tmp_dir: tmp_dir} do
      with_env("MIX_BUILD_PATH", Path.join([tmp_dir, ".expert", "build", "dev"]))

      source_file = Path.join([tmp_dir, "lib", "source_file.ex"])
      build_file = mix_build_file!(tmp_dir, "generated.ex", build_path: "custom_build")

      write_mix_project!(
        tmp_dir,
        "ConfiguredBuildProjectSourceTest.MixProject",
        ~s([app: :configured_build_project_source_test, version: "0.1.0", build_path: "custom_build"])
      )

      write_file!(source_file, "defmodule SourceFile do end")
      write_file!(build_file, "defmodule GeneratedBuildFile do end")

      project = project(tmp_dir)

      assert Paths.project_source?(project, source_file)
      refute Paths.project_source?(project, build_file)
    end

    @tag :tmp_dir
    test "excludes active in-root path dependencies", %{tmp_dir: tmp_dir} do
      app_root = Path.join(tmp_dir, "app")
      dep_root = Path.join([app_root, "deps", "dep"])
      app_file = Path.join([app_root, "lib", "app_module.ex"])
      dep_file = Path.join([dep_root, "lib", "dep_module.ex"])

      write_mix_project!(
        app_root,
        "PathDependencyProjectSourceTest.MixProject",
        ~s([app: :path_dependency_project_source_test, version: "0.1.0", deps: [{:dep, path: "deps/dep"}]])
      )

      write_mix_project!(
        dep_root,
        "PathDependencyProjectSourceTest.DepMixProject",
        ~s([app: :dep, version: "0.1.0"])
      )

      write_file!(app_file, "defmodule AppModule do end")
      write_file!(dep_file, "defmodule DepModule do end")

      project = project(app_root)

      assert Paths.project_source?(project, app_file)
      refute Paths.project_source?(project, dep_file)
    end
  end

  defp with_env(name, value) do
    original = System.fetch_env(name)
    System.put_env(name, value)

    on_exit(fn -> restore_env(name, original) end)
  end

  defp restore_env(name, {:ok, value}) do
    System.put_env(name, value)
  end

  defp restore_env(name, :error) do
    System.delete_env(name)
  end

  defp project(root) do
    root |> Forge.Document.Path.to_uri() |> Project.new()
  end

  defp write_file!(path, contents) do
    File.mkdir_p!(Path.dirname(path))
    File.write!(path, contents)
    path
  end

  defp write_mix_project!(root, module_name, project_config) do
    write_file!(Path.join(root, "mix.exs"), """
    defmodule #{module_name} do
      use Mix.Project

      def project do
        #{project_config}
      end
    end
    """)
  end

  defp mix_build_file!(root, relative_path, config \\ []) do
    build_root =
      File.cd!(root, fn ->
        config
        |> Keyword.put_new(:build_per_environment, true)
        |> Mix.Project.build_path()
        |> Path.dirname()
      end)

    Path.join([build_root | List.wrap(relative_path)])
  end
end
